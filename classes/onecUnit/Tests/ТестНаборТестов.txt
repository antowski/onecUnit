
Перем мРезультат;
Перем мПроверить;

Процедура Конструктор()
	мПроверить = СоздатьОбъект("Утверждения");
КонецПроцедуры // Конструктор

Процедура ПередВыполнением() Экспорт
	
КонецПроцедуры // ПередВыполнением

Процедура ПроверитьКолЗапущеноПровалено(пРезультат, пКолЗапущено, пКолПровалено)
	мПроверить.Равно(пКолЗапущено, пРезультат.ПолучитьКолЗапущено());
	мПроверить.Равно(пКолПровалено, пРезультат.ПолучитьКолПровалено());	
КонецПроцедуры // ПроверитьКолЗапущеноПровалено

Процедура тестНаборТестов() Экспорт
	
	НаборТестов = СоздатьОбъект("НаборТестов");
	НаборТестов.Добавить( НаборТестов.СоздатьТест("Фикстура.БылВыполнен", "ТестовыйМетод") );
	НаборТестов.Добавить( НаборТестов.СоздатьТест("Фикстура.БылВыполнен", "СломанныйТест") );
	
	Результат = СоздатьОбъект("РезультатТеста");
	НаборТестов.Выполнить(Результат);
	
	ПроверитьКолЗапущеноПровалено(Результат, 2, 1);
	
КонецПроцедуры // тестНаборТестов

Процедура тестОшибкаСозданияТеста_ЕслиКлассаТестаНеСуществует() Экспорт
	
	ИмяНесуществующегоКласса = "ИмяНесуществующегоКласса";
	ОжидаемаяОшибка = "Не обнаружен класс '" + ИмяНесуществующегоКласса + "'";
	
	ИсключениеВыброшено = 0;
	
	НаборТестов = СоздатьОбъект("НаборТестов");
	
	Попытка
		НаборТестов.СоздатьТест(ИмяНесуществующегоКласса, "НесуществующийМетод");
	Исключение
		ИсключениеВыброшено = 1;
		мПроверить.НеРавно(0, Найти(ОписаниеОшибки(), ОжидаемаяОшибка));
	КонецПопытки;
	
	мПроверить.Равно(1, ИсключениеВыброшено);
	
КонецПроцедуры // тестОшибкаСозданияТеста_ЕслиКлассаТестаНеСуществует

Процедура тестОшибкаСозданияТеста_ЕслиТестовогоМетодаНеСуществует() Экспорт
	
	ИсключениеВыброшено = 0;
	
	ИмяТестовогоКласса = "Фикстура.БылВыполнен";
	ИмяТестовогоМетода = "НесуществующийМетод";
	ОжидаемаяОшибка = "Не обнаружен метод '" + ИмяТестовогоМетода + "' в классе '" + ИмяТестовогоКласса + "'";
	
	НаборТестов = СоздатьОбъект("НаборТестов");
	
	Попытка
		НаборТестов.СоздатьТест(ИмяТестовогоКласса, ИмяТестовогоМетода);
	Исключение
		ИсключениеВыброшено = 1;
		мПроверить.НеРавно(0, Найти(ОписаниеОшибки(), ОжидаемаяОшибка));
	КонецПопытки;
	
	мПроверить.Равно(1, ИсключениеВыброшено);
	
КонецПроцедуры // тестОшибкаСозданияТеста_ЕслиТестовогоМетодаНеСуществует

Процедура тестУспешноеСозданиеНабораПоИмениТестовогоСлучая() Экспорт
	
	НаборТестов = СоздатьОбъект("НаборТестов");
	НаборТестов.ЗагрузитьИзТестовогоСлучая("Фикстура.ТестовыйСлучайСДвумяУспешнымиТестами");
	
	Результат = СоздатьОбъект("РезультатТеста");
	НаборТестов.Выполнить(Результат);
	
	ПроверитьКолЗапущеноПровалено(Результат, 2, 0);
	
КонецПроцедуры // тестУспешноеСозданиеНабораПоИмениТестовогоСлучая