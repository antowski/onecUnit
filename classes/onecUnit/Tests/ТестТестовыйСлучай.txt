
Перем мТест;
Перем мРезультат;
Перем мПроверить;

Процедура Конструктор()
	мПроверить = СоздатьОбъект("Утверждения");
КонецПроцедуры // Конструктор

Процедура ПередВыполнением() Экспорт
	мРезультат = СоздатьОбъект("РезультатТеста");
	мТест = СоздатьОбъект("Фикстура.БылВыполнен");
	мТест.Инит("ТестовыйМетод");
КонецПроцедуры // ПередВыполнением

Процедура тестВыполнениеТеста() Экспорт
	мТест.Выполнить(мРезультат);
	мПроверить.Равно("ПередВыполнением ТестовыйМетод ПослеВыполнения ", мТест.мЛог);	
КонецПроцедуры // тестВыполнениеТеста

Процедура тестРезультатТестПройден() Экспорт
	мТест.Выполнить(мРезультат);
	мПроверить.Равно("запущено: 1; провалено: 0", мРезультат.Сводка());	
КонецПроцедуры // тестРезультатТестПройден

Процедура тестРезультатТестПровален() Экспорт
	мТест.Инит("СломанныйТест");
	мТест.Выполнить(мРезультат);
	мПроверить.Равно("запущено: 1; провалено: 1", мРезультат.Сводка());
КонецПроцедуры // тестРезультатТестПровален

Процедура тестПослеВыполненияВызываетсяВСлучаеПровалаТеста() Экспорт
	мТест.Инит("СломанныйТест");
	мПроверить.Равно(0, мТест.мПослеВыполненияВызван);
	мТест.Выполнить(мРезультат);
	мПроверить.Равно(1, мТест.мПослеВыполненияВызван);
	мПроверить.Равно("запущено: 1; провалено: 1", мРезультат.Сводка());
КонецПроцедуры // тестПослеВыполненияВызываетсяВСлучаеПровалаТеста

Процедура тестПроваленЕслиЕстьОшибкаПередВыполнением() Экспорт
	мТест = СоздатьОбъект("Фикстура.ОшибкаПередВыполнением");	
	мТест.Инит("тестУспешный");
	мТест.Выполнить(мРезультат);
	мПроверить.Равно("запущено: 1; провалено: 1", мРезультат.Сводка());
КонецПроцедуры // тестПроваленЕслиЕстьОшибкаПередВыполнением
