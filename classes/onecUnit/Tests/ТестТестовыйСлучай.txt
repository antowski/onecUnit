
Перем мТест;
Перем мРезультат;
Перем мПроверить;

Процедура Конструктор()
	мПроверить = СоздатьОбъект("Утверждения");
КонецПроцедуры // Конструктор

Процедура ПроверитьЗапущенТолькоОдинТестИОнПровален(пРезультатТестирования)
	мПроверить.Равно(1, пРезультатТестирования.ПолучитьКолЗапущено());	
	мПроверить.Равно(1, пРезультатТестирования.ПолучитьКолПровалено());	
КонецПроцедуры

Процедура ПередЗапуском() Экспорт
	мРезультат = СоздатьОбъект("РезультатТеста");
	мТест = СоздатьОбъект("Фикстура.БылВыполнен");
	мТест.Инит("ТестовыйМетод");
КонецПроцедуры // ПередВыполнением

Процедура тестВыполнениеТеста() Экспорт
	мТест.Выполнить(мРезультат);
	мПроверить.Равно("ПередЗапуском ТестовыйМетод ПослеЗапуска ", мТест.мЛог);	
КонецПроцедуры // тестВыполнениеТеста

Процедура тестРезультатТестПройден() Экспорт
	мТест.Выполнить(мРезультат);
	мПроверить.Равно(1, мРезультат.ПолучитьКолЗапущено());	
	мПроверить.Равно(0, мРезультат.ПолучитьКолПровалено());	
КонецПроцедуры // тестРезультатТестПройден

Процедура тестРезультатТестПровален() Экспорт
	мТест.Инит("СломанныйТест");
	мТест.Выполнить(мРезультат);
	ПроверитьЗапущенТолькоОдинТестИОнПровален(мРезультат);
КонецПроцедуры // тестРезультатТестПровален

Процедура тестПослеВыполненияВызываетсяВСлучаеПровалаТеста() Экспорт
	мТест.Инит("СломанныйТест");
	мПроверить.Равно(0, мТест.мПослеВыполненияВызван);
	мТест.Выполнить(мРезультат);
	мПроверить.Равно(1, мТест.мПослеВыполненияВызван);	
КонецПроцедуры // тестПослеВыполненияВызываетсяВСлучаеПровалаТеста

Процедура тестПроваленЕслиЕстьОшибкаПередЗапуском() Экспорт
	мТест = СоздатьОбъект("Фикстура.ОшибкаПередЗапуском");	
	мТест.Инит("тестУспешный");
	мТест.Выполнить(мРезультат);
	ПроверитьЗапущенТолькоОдинТестИОнПровален(мРезультат);
КонецПроцедуры // тестПроваленЕслиЕстьОшибкаПередЗапуском
