
Процедура Провален(пТекстОшибки = "") Экспорт
	я()._ВыброситьИскл(пТекстОшибки);
КонецПроцедуры // Провален

Процедура Ложь(пЗначение) Экспорт
	Если ПустоеЗначение(пЗначение) = 0 Тогда
		Провален("" + пЗначение + " - ИСТИНА, а ожидалось ЛОЖЬ.");
	КонецЕсли;
КонецПроцедуры // Ложь

Процедура Истина(пЗначение) Экспорт
	Если ПустоеЗначение(пЗначение) = 1 Тогда
	    Провален("" + пЗначение + " - ЛОЖЬ, а ожидалось ИСТИНА.");
	КонецЕсли;
КонецПроцедуры

Процедура Равно(пЗначение1, пЗначение2) Экспорт
	
	Если ТипЗначения(пЗначение1) <> ТипЗначения(пЗначение2) Тогда
		Провален("типы значений не совпадают: '" + пЗначение1 + "' и '" + пЗначение2 + "'");
	КонецЕсли;
	
	Если пЗначение1 <> пЗначение2 Тогда
		Сообщение = "Значения НЕ равны: ";
		Если (СтрДлина(пЗначение1) > 20) ИЛИ (СтрДлина(пЗначение2) > 20) Тогда
			Сообщение = Сообщение + "
			|	'" + пЗначение1 + "'
			|	'" + пЗначение2 + "',
			|";
		Иначе
			Сообщение = Сообщение + "'" + пЗначение1 + "' и '" + пЗначение2 + "',";
		КонецЕсли;
		Провален(Сообщение);
	КонецЕсли;
	
КонецПроцедуры // Равно

Процедура НеРавно(пЗначение1, пЗначение2) Экспорт
	
	Если пЗначение1 = пЗначение2 Тогда
		Провален("Значения равны: '" + пЗначение1 + "' и '" + пЗначение2 + "', а ожидали что НЕ равны");
	КонецЕсли;
	
КонецПроцедуры // НеРавно

Процедура Содержит(ПроверяемаяСтрока, Подстрока) Экспорт
	
	Если (ТипЗначения(ПроверяемаяСтрока) <> 2) ИЛИ (ТипЗначения(Подстрока) <> 2) Тогда
		Провален("недопустимые типы значений: проверяемое значение и подстрока поиска должны иметь тип 'Строка'");
	КонецЕсли;
	
	Если Найти(ПроверяемаяСтрока, Подстрока) = 0 Тогда
		Провален("Ожидали что '" + ПроверяемаяСтрока + "' содержит '" + Подстрока + "'");
	КонецЕсли;
	
КонецПроцедуры // Содержит

Процедура ВыбрасываетИсключение(пОбъект, пИмяМетода, пСписокПараметров = "", пОжидаемоеИсключение = "") Экспорт
	
	Если пСписокПараметров = "" Тогда
		пСписокПараметров = СоздатьОбъект("СписокЗначений");
	КонецЕсли;
	
	сзПараметрыВызова = пСписокПараметров;
	
	Информатор = СоздатьОбъект("Информатор");
	
	Делегат = СоздатьОбъект("Делегат");
	Делегат.ОпределитьТип(сзПараметрыВызова.РазмерСписка(), Информатор.ЯвляетсяФункцией(пОбъект, пИмяМетода));
	Делегат.Добавить(пОбъект, пИмяМетода);
	
	ИсключениеВыброшено = 0;
	ТекстИсключения = "";
	
	Попытка
		Делегат.ВызватьСЗ(сзПараметрыВызова);		
	Исключение
		ИсключениеВыброшено = 1;
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	
	Если ИсключениеВыброшено = 0 Тогда
		Провален("Ожидали метод '" + пИмяМетода + "' выбросит исключение, а оно не выброшено");
	КонецЕсли;
	
	Если ПустаяСтрока(пОжидаемоеИсключение) = 0 Тогда
		Если Найти(ТекстИсключения, пОжидаемоеИсключение) = 0 Тогда
			Провален("ожидали что будет исключение с текстом """ + пОжидаемоеИсключение 
				+ """, а было исключение с текстом """ + ТекстИсключения + """");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
